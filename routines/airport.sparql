### Routines for cleaning up the airport kg

## Merge using the iata code, assign a new IRI
DELETE {
  ?sub ?p ?s
}
INSERT {
  ?sub ?p ?newIRI
}
WHERE {
    ?s <http://schema.org/iatacode> ?iata .
    ?sub ?p ?s .
    FILTER (strstarts(str(?s), "urn:skolem")) .
    BIND(IRI(CONCAT("urn:iata:", STR(?iata))) AS ?newIRI)
};

DELETE {
  ?s ?p ?o
}
INSERT {
  ?newIRI ?p ?o
}
WHERE {
  ?s <http://schema.org/iatacode> ?iata .
  ?s ?p ?o .
  FILTER (strstarts(str(?s), "urn:skolem")) 
  FILTER (?iata!="N/A" && ?iata!="")
  BIND(IRI(CONCAT("urn:iata:", STR(?iata))) AS ?newIRI)
}

## Drop iata code with N/A value
DELETE {
  ?s <http://schema.org/iatacode> ?iata .
}
WHERE {
  ?s <http://schema.org/iatacode> ?iata .
  FILTER(str(?iata) = "N/A") 
}

## Merge using icao code@advanced
SELECT ?s1 ?s2 WHERE  {
    ?s1 a <http://schema.org/Airport>.
    ?s2 a <http://schema.org/Airport>.
    ?s1 <http://schema.org/icaocode> ?o0. ?s2 <http://schema.org/icaocode> ?o0.
    FILTER(STR(?s1) < STR(?s2))
}
#
DELETE { ?sub ?pred {{s2}} }
INSERT { ?sub ?pred {{s1}} }
WHERE  { ?sub ?pred {{s2}} };
DELETE { {{s2}} ?p ?o }
INSERT { {{s1}} ?p ?o }
WHERE  { {{s2}} ?p ?o }