### Dealing with country entities

## Merge by name, assign a new iri
# redirect incoming triples
DELETE {
  ?s2 ?p ?s .
}
INSERT {
  ?s2 ?p ?newIRI .
}
WHERE {
  ?s a <http://schema.org/Country> ;
     <http://schema.org/name> ?country .
  ?s2 ?p ?s .
  FILTER(?s2 != ?s)
  BIND(REPLACE(STR(?country), "\\s+", "_") AS ?cleanName)
  BIND(IRI(CONCAT("urn:Country:", ?cleanName)) AS ?newIRI)
};

# copy from old to new
DELETE {
  ?s ?p ?o
}
INSERT {
  ?newIRI ?p ?o
}
WHERE {
  ?s a <http://schema.org/Country> ;
     <http://schema.org/name> ?country ;
     ?p ?o .
  FILTER(isIRI(?s))
  BIND(REPLACE(STR(?country), "\\s+", "_") AS ?cleanName)
  BIND(IRI(CONCAT("urn:Country:", ?cleanName)) AS ?newIRI)
}

## Merge countries using name@advanced
SELECT ?s1 ?s2 WHERE {
  ?s1 a <http://schema.org/Country> ;
      <http://schema.org/name> ?name .
  ?s2 a <http://schema.org/Country> ;
      <http://schema.org/name> ?name .
  FILTER(str(?s1) < str(?s2))  # Choose deterministic pair direction
}
#
DELETE {?sub ?pred {{s2}}}
INSERT {?sub ?pred {{s1}}}
WHERE  {?sub ?pred {{s2}}};
DELETE {{{s2}} ?p ?o}
INSERT {{{s1}} ?p ?o}
WHERE  {{{s2}} ?p ?o}